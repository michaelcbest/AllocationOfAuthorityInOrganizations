
* Wiper - ItemID938

use 		"${rawdata}/POPSData/NewItemID938.dta", clear
local 		ItemID = NewItemID
ren			GroupFinal Treatment

drop 		if UnitPrice==.
count
global NObs = `r(N)'
summ UnitPrice, meanonly
global mPrice = `r(mean)'
count if Treatment == 4
global NObsControl = `r(N)'
summ UnitPrice if Treatment == 4, meanonly
global mPriceControl = `r(mean)'
gen Value = UnitPrice * Quantity
summ Quantity, meanonly
global mQuantity = `r(mean)'
summ Quantity if Treatment == 4, meanonly
global mQuantityControl = `r(mean)'
summ Value, meanonly
global mValue = `r(mean)'
summ Value if Treatment == 4, meanonly
global mValueControl = `r(mean)'
drop 		if UnitPrice < 12
drop		if UnitPrice > 800

global 		Attributes = "BRAND COUNTRY_OF_ORIGIN HANDLE_LENGTH_FT HANDLE_MATERIAL WIPER_MATERIAL"
global 		NumberOfAttributes = 5

global 		IVar = "BRAND COUNTRY_OF_ORIGIN HANDLE_LENGTH_FT HANDLE_MATERIAL WIPER_MATERIAL Unit"
global 		NVar = ""

foreach 	var of varlist $Attributes {
label 		var `var' "`var'"
}

count
local 		totalobs = `r(N)'
_pctile		lUnitPrice, percentiles(1 99)
local 		lp = `r(r1)'
local 		up = `r(r2)'

count 		if lUnitPrice > `lp' & lUnitPrice <= `up'

if 			`r(N)' > (0.95 * `totalobs') {
			gen TrimmedSample = (lUnitPrice > `lp' & lUnitPrice <= `up')
}
else {
_pctile		lUnitPrice, percentiles(0.5 99.5)
local 		lp = `r(r1)'
local 		up = `r(r2)'

count 		if lUnitPrice > `lp' & lUnitPrice <= `up'
if 			`r(N)' > 0.95 * `totalobs' {
gen			TrimmedSample = (lUnitPrice > `lp' & lUnitPrice <= `up')
}
else {
gen TrimmedSample = 1
}
}

gen 		InReg = (inlist(Treatment,4,5) & TrimmedSample == 1)

/***
###Cleaning Attributes 

1-Some attribute values are duplicated with slightly different spelling 
2-Others are infrequent. 
3-Others only occur in groups 1-3
This part of code groups the attribute values. 

BRAND
***/

di "Before Cleaning"
tab BRAND InReg

qui levelsof BRAND, local(brands)
foreach		brand in `brands'	{
			di _n `"`brand'"'
			count if BRAND == `"`brand'"' & InReg == 1
			if	`r(N)' < 2	{			  
			  replace BRAND = "" if BRAND == `"`brand'"'
			}
}

di "After Cleaning"
tab BRAND InReg


/***
####COUNTRY_OF_ORIGIN
***/

di "Before Cleaning"
tab COUNTRY_OF_ORIGIN InReg

replace COUNTRY_OF_ORIGIN = "CHINA" if COUNTRY_OF_ORIGIN == "UK"

di "After Cleaning"
tab COUNTRY_OF_ORIGIN InReg


/***
####HANDLE_LENGTH_FT
***/

di "Before Cleaning"
tab HANDLE_LENGTH_FT InReg

replace HANDLE_LENGTH_FT = "2 FT" if inlist(HANDLE_LENGTH_FT,"1 FT","1.5 FT")
replace HANDLE_LENGTH_FT = "4 FT" if HANDLE_LENGTH_FT == "3.5 FT"

di "After Cleaning"
tab HANDLE_LENGTH_FT InReg


/***
####HANDLE_MATERIAL
***/

di "Before Cleaning"
tab HANDLE_MATERIAL InReg

replace HANDLE_MATERIAL = "" if HANDLE_MATERIAL == "ROPE"

di "After Cleaning"
tab HANDLE_MATERIAL InReg


/***
####WIPER_MATERIAL
***/

di "Before Cleaning"
tab WIPER_MATERIAL InReg

replace WIPER_MATERIAL = "CLOTH" if WIPER_MATERIAL == "COTTON"
replace WIPER_MATERIAL = "MOP" if inlist(WIPER_MATERIAL,"DORY","JHALLAR","SUTRI")
qui levelsof WIPER_MATERIAL, local(materials)
foreach		material in `materials'	{
			di _n `"`material'"'
			count if WIPER_MATERIAL == `"`material'"' & InReg == 1
			if	`r(N)' < 2	{			  
			  replace WIPER_MATERIAL = "" if WIPER_MATERIAL == `"`material'"'
			}
}

di "After Cleaning"
tab WIPER_MATERIAL InReg

codebook 	$Attributes

foreach 	var of varlist $Attributes {
capture 	gen m`var' = (`var' == .)
capture 	gen m`var' = (`var' == "")
} 

egen 		NMissing = rowtotal(m*)
drop 		if NMissing == $NumberOfAttributes
	
/***
Dropping variables that have fewer than 20 observations from the regression
***/

global		IVar_reg = ""
foreach		var of varlist $IVar {
di			"`var'"
count		if `var' != ""
			if			`r(N)' >= 20	{
						global		IVar_reg = "${IVar_reg}" + " " + "`var'"
			}
}

global		NVar_reg = ""
capture foreach		var of varlist $NVar {
di			"`var'"
count		if `var' != .
			if			`r(N)' >= 20	{
						global		NVar_reg = "${NVar_reg}" + " " + "`var'"
			}
}


/***

Repacing the missing values with the newly defined category "MISSING" for categorical variables 
and 0 for numeric variables. 

***/

foreach 	var of varlist $Attributes {

			capture confirm string var `var'
			if _rc==0 {
			replace `var' = "00_MISSING" if `var' == "" | `var' == " " |`var' == "	" | `var' == ","
			}
			else {
			replace `var' = 0 if `var' == . 
			}
			tab 	`var', m
}
		

/***
Encoding the variables so that they can be treated as categorical variable in regression. 
***/

foreach 		var of varlist Department District $IVar_reg {
			capture confirm string var `var'
			if _rc==0 {
			encode 	`var', gen(`var'_enc)
			drop 		`var'
			ren 	`var'_enc `var'
			}
}
rename 		DeliveryDate time

*Build the RHS of the regression for the numeric variables
global		NRHS = ""
if			strlen("${NVar_reg}") > 1 {
foreach		var of varlist $NVar_reg {
			global		NRHS = "${NRHS}" + " " + "`var'" + " m" + "`var'"
}
}
save 			"${tempdir}/Item`ItemID'.dta", replace

/*				Save the variables used into a dataset. 		*/
clear
set			obs 1
gen			ItemID = `ItemID'

global		IVarList = ""
foreach		var in $IVar {
			*global 		IVarList = "${IVarList}" + " " + "`var'" + "_Item`ItemID'"
			global 		IVarList = "${IVarList}" + " " + "`var'"
}
gen			AllIVars = "${IVarList}"

global		NVarList = ""
capture foreach		var in $NVar {
			*global 		NVarList = "${NVarList}" + " " + "`var'" + "_Item`ItemID'"
			global 		NVarList = "${NVarList}" + " " + "`var'"
}
gen			AllNVars = "${NVarList}"


global		IVarList_reg = ""
foreach		var in $IVar_reg {
			*global 		IVarList_reg = "${IVarList_reg}" + " " + "`var'" + "_Item`ItemID'"
			global 		IVarList_reg = "${IVarList_reg}" + " " + "`var'"
}
gen			RegIVars = "${IVarList_reg}"

global		NVarList_reg = ""
capture foreach		var in $NVar_reg {
			*global 		NVarList_reg = "${NVarList_reg}" + " " + "`var'" + "_Item`ItemID' " + "m`var'_Item`ItemID'"
			global 		NVarList_reg = "${NVarList_reg}" + " " + "`var'" + " " + "m`var'"
}
gen			RegNVars = "${NVarList_reg}"
gen 		NObs = ${NObs}
gen			mPrice = ${mPrice}
gen 		NObsControl = ${NObsControl}
gen			mPriceControl = ${mPriceControl}
gen     mQuantity = ${mQuantity}
gen     mQuantityControl = ${mQuantityControl}
gen     mValue = ${mValue}
gen     mValueControl = ${mValueControl}
gen 		NumberOfAttributes = ${NumberOfAttributes}

save		"${tempdir}/RegAttributes_Item`ItemID'.dta", replace

